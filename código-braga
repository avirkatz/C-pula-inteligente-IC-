#include <FastLED.h>
#include "DHT.h"

#define PIN_LEDS 6
#define NUM_LEDS 16

#define PIN_DHT 2
#define DHTTYPE DHT11

#define PIN_LDR A0

const int pinoTrig = 9;
const int pinoEcho = 10;

CRGB leds[NUM_LEDS];
DHT dht(PIN_DHT, DHTTYPE);

const CRGB corAzulClaro   = CRGB(50, 150, 255);
const CRGB corLaranja     = CRGB(255, 100, 0);
const CRGB corAmarelo     = CRGB(255, 255, 0);
const CRGB corVermelho    = CRGB(255, 0, 0);
const CRGB corCiano       = CRGB(0, 255, 255);
const CRGB corAzul        = CRGB(0, 0, 255);

const CRGB corBranco      = CRGB(255, 255, 255);
const CRGB corVerdeEscuro = CRGB(0, 120, 0);
const CRGB corRoxo        = CRGB(160, 0, 255);

const CRGB corVerde       = CRGB(0, 255, 0);
const CRGB corApagado     = CRGB(0, 0, 0);

const int LIMIAR_ESCURO   = 300;
const int LIMIAR_MODERADO = 700;

float umidade = NAN;
float temperatura = NAN;
int luminosidade = 0;
int distanciaCm = 0;

unsigned long tDHT = 0;
unsigned long tLDR = 0;
unsigned long tUS  = 0;

const unsigned long DT_DHT = 2000; // DHT11: 2s é o ideal
const unsigned long DT_LDR = 200;  // LDR pode ser mais rápido
const unsigned long DT_US  = 100;  // Ultrassônico mais rápido

void pintaFaixa(int ini, int fim, const CRGB &cor) {
  for (int i = ini; i <= fim; i++) leds[i] = cor;
}

int lerDistanciaCm() {
  digitalWrite(pinoTrig, LOW);
  delayMicroseconds(2);
  digitalWrite(pinoTrig, HIGH);
  delayMicroseconds(10);
  digitalWrite(pinoTrig, LOW);

  unsigned long dur = pulseIn(pinoEcho, HIGH, 30000UL); // ~30ms timeout
  if (dur == 0) return 0;
  return (int)(dur / 58);
}

void atualizaTemperatura() {
  if (isnan(temperatura)) {
    pintaFaixa(0, 3, corAzulClaro);}
  if (temperatura < 20)       pintaFaixa(0, 3, corAzulClaro);
  else if (temperatura < 25)  pintaFaixa(0, 3, corLaranja);
  else if (temperatura < 30)  pintaFaixa(0, 3, corAmarelo);
  else                        pintaFaixa(0, 3, corVermelho);
}

void atualizaUmidade() {
  if (isnan(umidade)) {
    pintaFaixa(4, 7, corAzulClaro);
    return;}

  if (umidade < 40)          pintaFaixa(4, 7, corLaranja);
  else if (umidade <= 70)    pintaFaixa(4, 7, corCiano);
  else                       pintaFaixa(4, 7, corAzul);
}

void atualizaLuminosidade() {
  if (luminosidade < LIMIAR_ESCURO)        pintaFaixa(8, 11, corBranco);
  else if (luminosidade < LIMIAR_MODERADO) pintaFaixa(8, 11, corVerdeEscuro);
  else                                     pintaFaixa(8, 11, corRoxo);
}

void atualizaProximidade() {

  if (distanciaCm > 0 && distanciaCm < 10) pintaFaixa(12, 15, corVermelho);
  else if (distanciaCm >= 10 && distanciaCm < 30) pintaFaixa(12, 15, corAmarelo);
  else if (distanciaCm >= 30 && distanciaCm < 50) pintaFaixa(12, 15, corVerde);
  else pintaFaixa(12, 15, corAzulClaro); 
}

void setup() {
  Serial.begin(9600);

  pinMode(pinoTrig, OUTPUT);
  pinMode(pinoEcho, INPUT);

  dht.begin();

  FastLED.addLeds<WS2812B, PIN_LEDS, GRB>(leds, NUM_LEDS);
  FastLED.setBrightness(80);

  FastLED.clear();
  FastLED.show();

  Serial.println(F("Sistema integrado (DHT + LDR + Ultrassonico + LEDs) iniciado!"));
}

void loop() {
  unsigned long agora = millis();

  // 1) Atualiza leituras em tempos diferentes
  if (agora - tDHT >= DT_DHT) {
    tDHT = agora;
    umidade = dht.readHumidity();
    temperatura = dht.readTemperature();
  }

  if (agora - tLDR >= DT_LDR) {
    tLDR = agora;
    luminosidade = analogRead(PIN_LDR);
  }

  if (agora - tUS >= DT_US) {
    tUS = agora;
    distanciaCm = lerDistanciaCm();
  }

  atualizaTemperatura();
  atualizaUmidade();
  atualizaLuminosidade();
  atualizaProximidade();

  FastLED.show();

  static unsigned long tLog = 0;
  if (agora - tLog >= 500) {
    tLog = agora;

    Serial.print(F("Temp: "));
    if (isnan(temperatura)) Serial.print(F("ERRO"));
    else Serial.print(temperatura);

    Serial.print(F(" C | Umid: "));
    if (isnan(umidade)) Serial.print(F("ERRO"));
    else Serial.print(umidade);

    Serial.print(F(" % | Luz(A0): "));
    Serial.print(luminosidade);

    Serial.print(F(" | Dist: "));
    if (distanciaCm == 0) Serial.println(F("FORA"));
    else { Serial.print(distanciaCm); Serial.println(F(" cm")); }
  }
}
